اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/سندات"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Stripe"، "سـترايب.أسس")؛
استخدم مـتم؛

عرف وكيل: سـترايب.وكـيل(نـص(نـظام.هات_متغير_محيطي("STRIPE_KEY")))؛
وكيل.إطناب = 0؛

طـرفية.اطبع("إنشاء زبون...\n")؛
عرف معرف_الزبون: نـص = وكيل.أنشئ_زبونا(نـص("email=super211@gmail.com&name=sonicmrah"))؛
طـرفية.اطبع("معرف الزبون المضاف: %s\ج"، معرف_الزبون.صوان)؛
طـرفية.اطبع("أُنشئ الزبون\ج\ج")؛

طـرفية.اطبع("الزبائن الحاليين:\ج")؛
عرف الزبائن: مـصفوفة[سـندنا[سـترايب.زبـون]]؛
الزبائن = وكيل.هات_الزبائن()؛
عرف ع: صـحيح؛
لكل ع = 0، ع < الزبائن.هات_الطول()، ع++ {
    طـرفية.اطبع(
        "المعرف: %s\ج\tالاسم: %s\ج\tالبريد الإلكتروني: %s\ج\ج"،
        الزبائن(ع).المعرف.صوان، الزبائن(ع).الاسم.صوان، الزبائن(ع).البريد_الإلكتروني.صوان
    )؛
}

طـرفية.اطبع("مصفوفة الرصيد:\ج")؛
عرف الرصيد : مـصفوفة[سـندنا[سـترايب.رصـيد]]؛
الرصيد = وكيل.هات_الرصيد()؛
عرف م: صـحيح؛
لكل م = 0، م < الرصيد.هات_الطول()، م++ {
    طـرفية.اطبع(
        "النوع: %s\جالعملة: %s\جالمبلغ: %f\جالمصادر:\ج"،
        الرصيد(م).نوع_الرصيد.صوان، الرصيد(م).العملة.صوان، الرصيد(م).المبلغ
    )؛
    عرف ع: صـحيح = 0؛
    لكل ع = 0، ع < الرصيد(م).المصادر.هات_الطول()، ع++ {
        طـرفية.اطبع(
            "    نوع المصدر: %s\ج"
            "    المبلغ: %f\ج"،
            الرصيد(م).المصادر(ع).النوع.صوان، الرصيد(م).المصادر(ع).المبلغ
        )
    }
    طـرفية.اطبع("\ج")؛
}
