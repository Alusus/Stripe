@merge module Stripe {
    func postRecord (key: String, url: String, body: String, verbose: Bool, retPropName: CharsPtr): Possible[String] {
        def request: Net.Request(url, key, String("Bearer"), String("application/x-www-form-urlencoded"));
        request.verbose = verbose;
        request.post(body);
        if request.responseHttpStatus == 401 {
            return Possible[String].failure(Errors.unauthenticated());
        } else if request.responseHttpStatus == 400 {
            def json: Json = request.responseBody;
            return Possible[String].failure(Errors.invalidParam(json("error")("param")));
        } else if request.responseHttpStatus == 0 {
            return Possible[String].failure(Errors.connection());
        } else if request.responseHttpStatus != 200 {
            return Possible[String].failure(Errors.unexpected());
        }
        def json: Json = request.responseBody;
        return Possible[String].success(json(retPropName));
    }

    func postRecord (key: String, url: String, body: String, verbose: Bool): Possible[String] {
        return postRecord(key, url, body, verbose, "id");
    }
}
